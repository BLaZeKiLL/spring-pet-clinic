buildscript {
    dependencies {
        classpath 'ro.isdc.wro4j.gradle:wro4j-gradle-plugin:1.8.0.Beta4'
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.1'
}

apply plugin: 'wro4j'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

def versionJQuery = '3.4.1'
def versionBootstrap = '3.4.1'
def versionJQueryUI = '1.12.1'

webResources {
    bundle ('core') {
        js 'js/**/*.js'
        preProcessor 'jsMin'
    }

    bundle ('libs') {
        js "webjars/jquery/$versionJQuery/jquery.min.js"
        js "webjars/jquery-ui/$versionJQueryUI/jquery-ui.min.js"
        js "webjars/bootstrap/$versionBootstrap/js/bootstrap.min.js"
    }

    bundle ('petclinic') {
        css "webjars/bootstrap/$versionBootstrap/less/bootstrap.less"

        css "less/petclinic.less"

        preProcessor 'lessCssImport'
        postProcessor 'less4j'
        cssRewriteUrl()
    }
}

dependencies {
    implementation project(':pet-clinic-data')

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.webjars:webjars-locator-core'

    webjars "org.webjars:jquery:$versionJQuery"
    webjars "org.webjars:jquery-ui:$versionJQueryUI"
    webjarsRuntime ("org.webjars:bootstrap:$versionBootstrap") {
        transitive = false
    }

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
